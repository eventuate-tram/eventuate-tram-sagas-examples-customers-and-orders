
apply plugin: 'maven-publish'

if (!springBootVersion.startsWith("3")) {
    apply plugin: 'spring-cloud-contract'
    contractTest {
        useJUnitPlatform()
        packageWithBaseClasses = 'io.eventuate.examples.tram.sagas.ordersandcustomers.customers.web'
    }

    publishing {
        repositories {
            maven { url "${project.rootDir}/build/repo" }
        }
        publications {
            stubs(MavenPublication) {
                artifactId "${project.name}-stubs"
                artifact verifierStubsJar

                pom {
                    packaging "jar"
                }
            }
        }
    }


    check.dependsOn(contractTest)
}



dependencies {
    implementation "io.eventuate.examples.common:eventuate-examples-common-money-jakarta9:$eventuateCommonExamplesVersion"
    implementation project(":customer-service:customer-service-api-web")
    implementation project(":customer-service:customer-service-domain")

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    testImplementation "org.springframework.cloud:spring-cloud-starter-contract-verifier"
    testImplementation "io.rest-assured:spring-mock-mvc"

}
